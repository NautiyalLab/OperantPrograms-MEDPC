\ 			C O M M E N T S 
 
\ CRF 60 Trial 60 Minutes 
\ Eric Teboul 7/31/2017 
\ Written for RNAC boxes. Check inputs and outputs 

\only save W variable to data file*******
DISKFORMAT = 13
DISKVARS = E,L,W     
DISKCOLUMNS = 1 

\*********** INPUTS ************* 
^headIN = 4        \  IR head entry detector
^NoseThing = 3 	  \  Nose port head entry detector 
^LLever = 2       \  left lever press
^RLever = 1       \  right lever press


\*********** OUTPUTS ************* 
^leftlever = 8 
^rightlever = 7 
^leftlight = 5
^rightlight = 4
^NoseThingLight = 2
^dipper = 6       \ On Liquid Dipper
^house = 1         \houselight 


\  VARIABLES USED IN THIS PROGRAM
\   a = 
\   b = 
\   c = 
\   d = ITI duration, selected from list i
\   e = 
\   f = 
\   g = head entries 
\   h = 
\   i = 
\   j = 
\   k = session duration clock 
\   l = dippers 
\   m =
\   n = nose pokes 
\   o = trial duration clock 
\   p = general counter 
\   q = 
\   r = 
\   s =
\   t = master timer 
\   u = 
\   v =  
\  (w) = time and event code for matlab 
\   x =
\   y = trials 
\   z =


\  \  SHOW POSITIONS
\  1 = Session time (minutes)  
\  2 = Trial Counter 
\  3 = Nose Pokes 
\  4 = Trial Time 
\  5 = Dippers 
\  6 = Head Entries 

\======================================================
\ MATLAB standardized codes
\ *** = used in this program

^Feed1 = 0021
^Feed2 = 0022
^Feed3 = 0023
^Feed4 = 0024
^DipOn = 0025   \***
^DipOff = 0026   \***
^LLeverOn = 0027 \***
^RLeverOn= 0028  \***
^LLeverOff = 0029 \***
^RLeverOff = 0030 \***
^LightOff1 = 0031
^LightOff2 = 0032
^LightOff3 = 0033
^LightOff4 = 0034
^LightOn1 = 0041
^LightOn2 = 0042
^LightOn3 = 0043
^LightOn4 = 0044
^ToneOff1 = 0051
^ToneOff2 = 0052
^ToneOff3 = 0053
^ToneOff4 = 0054
^ToneOn1 = 0061
^ToneOn2 = 0062
^ToneOn3 = 0063
^ToneOn4 = 0064
^WNoiseOff1 = 0071
^WNoiseOff2 = 0072
^WNoiseOff3 = 0073
^WNoiseOff4 = 0074
^WNoiseOn1 = 0081
^WNoiseOn2 = 0082
^WNoiseOn3 = 0083
^WNoiseOn4 = 0084
^NoseThingON = 0085
^NoseThing = 0086 
^NoseThingLight = 0087 
^NoseThingOFF = 0088 
^NoseThingLightOFF = 0089
^NoseThingLightON = 0090  
^LDipOn = 0091   
^LDipOff = 0092 
^RDipOn = 0093 
^RDipOff = 0094 
^LeftLeverOFF = 0095 
^RightLeverOFF = 0096 

^StartTrial = 0111
^EndTrial = 0112
^StartSession = 0113  \***
^EndSession = 0114   \***
^HouseLightOff = 0115
^HouseLightOn = 0116

^StartIti = 0121
^EndIti = 0122
^LightOffStan = 0201
^LightOnStan = 0202
^LightOffComp = 0203
^LightOnComp = 0204
^LightOffCtrl = 0205
^LightOnCtrl = 0206
^FeedStan = 0207
^FeedComp = 0208
^StartStanFlash = 0209
^StopStanFlash = 0211
^StartCompFlash = 0212
^StopCompFlash = 0213
^STNonGapNonProbe = 0214
^STNonGapProbe = 0215
^STGapNonProbe = 0216
^STGapProbe = 0217

^ProbeTrial = 0221
^AutoTrial = 0222

^PokeOff1 = 1001
^PokeOff2 = 1002
^PokeOff3 = 1003
^PokeOff4 = 1004
^PokeOn1 = 1011
^PokeOn2 = 1012
^PokeOn3 = 1013
^PokeOn4 = 1014
^LPressOn = 1015
^RPressOn = 1016
^LPressOff = 1017
^RPressOff = 1018
^PokeOffStan = 1301
^PokeOnStan = 1302
^PokeOffComp = 1303
^PokeOnComp = 1304
^PokeOffCtrl = 1305
^PokeOnCtrl = 1306


\================================================================


\Dimensionalize Event and Time code array
dim w=10000
dim a=2
dim b=2
dim e=3
dim k=4
dim r=2 
dim o=4

\list of delay durations (in hundredths of seconds) *change this to change delay durations
list h = 200

\ITI duration list (in hundredths of seconds)
list i = 500

\list of alternate lever options at start of new trial 
list g = 1, 2 


\===============================
\   Z-PULSES USED IN THIS PROGRAM
\ z1  = 
\ z2  = 
\ z3  = shut down after required dippers 
\ z4  = 
\ z5  = 
\ z6  = dipper delivery  
\ z9  = finish dipper delivery, start ITI 
\ z12 = restart trial clock   
\=============================== 

s.s.1,  \MATLAB TIMER "T"
s1,
        0": SET p = -1; set m = 1--->s2
s2,
        .01": SET T = T + 100000 --->sx \Keeps time in 100th of a second

S.S.3,    \INITIALIZE
s1,
    #start:    add p; set w(p)= ^StartSession+T;--->s2
s2,
    #z3: off^NoseThing; off^NoseThingLight; 
         add p; set w(p) = ^NoseThingOff+T;
         add p; set w(p) = ^NoseThingLightoff+T; --->s3
    60': off^NoseThing; off^NoseThingLight; 
         add p; set w(p) = ^NoseThingOff+T;
         add p; set w(p) = ^NoseThingLightoff+T; --->s3
s3,
    10": OFF^house; add p; set w(p) = ^HouseLightOFF+T;    
        add p; set w(p)= ^EndSession + T;
        add p; set w(p)=-987.987;--->stopabortflush


S.S.2,    \Trials
s1,
     #START: set u=1 ---> s2 
s2, 
       d#t:  z13; on^house; add p; set w(p)= ^HouseLightON+T; 
	     on^NoseThingLight; add p; set w(p) = ^NoseThingLightON+T; 
	     on^NoseThing; add p; set w(p) = ^NoseThingON+T; 
	     add y; show 2, Trials, y; set v=1; z12 ---> s3 
s3, 
     #R^NoseThing: add n; show 3, Nose Pokes, n; z2; add u; 
		   add p; set w(p) = ^Nosething+T;  ---> s4
s4, 
     0.5": Off^NoseThing; off^NoseThingLight; ---> s5

s5, 
     #z9: off^house; 
          add p; set w(p) = ^NoseThingOFF+T; 
          add p; set w(p) = ^NoseThingLightOFF+T; 
	  add p; set w(p) = ^HouseLightOFF+T;   
		   if y>59 [@end, @more]
                       @end: z3 --->s1
           	       @more: randd d=i ---> s2 
S.S.7 \Lever Release 
s1, 
     #Z2: randd d=g; if d=1 [@yes, @no] 
	   @yes: on^leftlever; add p; set w(p)= ^LLeveron+T; ---> s2 
	   @no: on^rightlever; add p; set w(p)= ^RLeveron+T; ---> s2 
s2, 
     #R^LLever: z6; set f=1; add p; set w(p) = ^LPressOn+T; ---> s3 
     #R^RLever: z1; set f=2; add p; set w(p) = ^RPressOn+T; ---> s3 

s3, 
     0.5": if f=1, [@yes, @no] 
	 @yes: off^leftlever; add p; set w(p)=^leftleverOFF+T ---> s1 
	 @no:  off^rightlever; add p; set w(p)=^rightleverOFF+T ---> s1   	 

S.S.6,  \Head Entry (Input 3) COUNTER
s1,
     #START:--->S2
s2,
      #R^headIN: add x; show 6, HE, x; 
                 add p; set w(p) = ^PokeOn1 + T---> sx


S.S.5, \Dipper Delivery Left 
s1,
    #z6:      ON^dipper; add p;set w(p)=^LdipON+T;
               add l;Show 5, Dippers, l; --->s2
s2,
    5":    OFF^dipper;add p;set w(p)=^LDipOff+T; z9 --->s1 

S.S.11, \Dipper Delivery Right 
s1,
    #z1:      ON^dipper; add p;set w(p)=^RdipON+T;
               add l;Show 5, Dippers, l; --->s2
s2,
    5":    OFF^dipper;add p;set w(p)=^RDipOff+T; z9 --->s1 
    
    
S.S.8, \Session Duration Clock
s1,
    #start:--->s2
s2,
    0":    set k(0)=k(0)+.01; if k(0)>.59 [@AddMin, @NoAdd]
            @AddMin: add k(1); set k(0)=0; set k(2)=k(0)+k(1);
                 show 1, Session, k(2)--->s2
            @NoAdd: set k(2)=k(0)+k(1); show 1, Session, k(2);--->s2

S.S.10, \Trial Duration Clock  
s1,
    #start:--->s2
s2,
    #z12:--->s3 
s3, 
    0": if u<2 [@continue, @waitfortrial] 
   	    @continue: set o(0)=o(0)+.01; if o(0)>.59 [@AddMin, @NoAdd]
            		@AddMin: add o(1); set o(0)=0; set o(2)=o(0)+o(1);
                 		 show 4, Trial Duration, o(2); --->s3
            		@NoAdd: set o(2)=o(0)+o(1); show 4, Trial Duration, o(2) ---> s3 
	    @waitfortrial: set o(0)=o(0)+0; set o(1)=o(1)+0; set o(2)=o(2)+0 ---> s4 
s4, 
    #z13: set o(0)=0; set o(1)=0; set o(2)=0 ---> s5 
s5, 
    0": set o(0)=o(0)+.01; if o(0)>.59 [@AddMin, @NoAdd]
            		@AddMin: add o(1); set o(0)=0; set o(2)=o(0)+o(1);
                 		 show 4, Trial Duration, o(2); --->s5
            		@NoAdd: set o(2)=o(0)+o(1); show 4, Trial Duration, o(2) ---> s5





	
