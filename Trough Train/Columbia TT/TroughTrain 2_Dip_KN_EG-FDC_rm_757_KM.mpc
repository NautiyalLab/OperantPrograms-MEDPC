\                        C O M M E N T S
\
\ Variable Time Feeder Training ~mean 45 sec.
\ 8 sec dip duration--->iti
\
\
\
\
\ Gray Herzberg 4/27/04; modified 6/20/12 Kathleen Taylor

\only save W, P variable to data file*******
DISKFORMAT = 13.1
DISKVARS = a, W    \"DISKVARS" only prints selected variables to datafile

                  \  Inputs ****************************
^headIN = 4       \  IR head entry detector
^headOUT = 5      \ IR head exit detector
^LLever2 = 2       \  left lever press
^RLever2 = 1       \  right lever press


                  \  Outputs ****************************
^fan = 12         \ Fans for Box ventilation
^dipper = 6        \ On Liquid Dipper
^feederlight = 8   \ Light in the Head Entry Hole; Signal being used in this exp.
^house = 1         \houselight
^trigger = 9
^triglight = 10 \Take a separate houselight outside of the box to use as a "trigger ON/OFF" indicator


    \CONSTANTS***********************
^DipDuration = 5   \dipper up duration (sec)

\  VARIABLES USED IN THIS PROGRAM
\   a = array of poke latencies while dipper is up
\   b = counter of pokes while dipper is up
\   c = session clock (displayed in Column 1)
\   d = poke latency timer (reset with every trial)
\   e = # head entries per dipper, reset with each dip
\   f
\   g
\   h
\   i = list array of ITI durations
\   j = Number of dippers
\   k
\   l
\   m
\   n
\   o
\   p = counter for w array
\   q
\   r = holder for reinforcers retreived
\   s = counter for reinforcers retreived
\   t
\   u
\   v
\  (w) = time and event code for matlab
\   x = ITI dur, selected from list i
\   y
\   z (reserved)


\  SHOW POSITIONS
\  1 = Session Timer
\  2 = Dipper Timer
\  3 = dipper counter
\  4 =
\  5 = head entries
\  6 =


\======================================================
\ MATLAB standardized codes

^Feed1 = 0021

^DipOn = 0025
^DipOff = 0026

^StartTrial = 0111
^EndTrial = 0112

^StartSession = 0113
^EndSession = 0114

^HouseLightOff = 0115
^HouseLightOn = 0116

^ToneOn1 = 0061

^Ontrigger= 4491
^Offtrigger= 4492

^Ontriglight = 4493
^Offtriglight = 4494

^PokeOff1 = 1001
^PokeOn1 = 1011


\================================================================


\Dimensionalize Event and Time code array
dim a=1000
dim w=10000
dim c=3

\ITI duration list (in hundredths of seconds)
list i = 720, 750, 920, 950, 1050, 1200, 1370, 1600, 2840, 3150, 3330, 3700, 3980, 5620, 5640, 6640, 7100, 9880
\===============================
\   Z-PULSES USED IN THIS PROGRAM
\ z1 = dipper up (initiate timer D)
\===============================

s.s.1,  \MATLAB TIMER "T"
s1,
        0":     SET p = -1; set b = -1--->s2
s2,
        .02":     SET T = T + 100000 --->sx  \Keeps time in 50th of a second

S.S.2,    \INITIALIZE
s1,
        #start:    ~initANL926;~;~SetRack (MG, 2);~;
                   ~SetAmp (MG,BOX, 90);~;~SetRF (MG,BOX, 10);~;~SetFreq (MG,BOX, 2500);~;

                   ON^house; add p; set w(p)= ^HouseLightOn + T;
                ON^triglight; add p; set w(p)= ^Ontriglight + T;
                ON^trigger; add p; set w(p)= ^ontrigger + T ---> s2

s2,
    5': OFF^trigger; add p; set w(p) = ^OFFtrigger+T;
        OFF^triglight; add p; set w(p) = ^OFFtriglight+T---> sx


S.S.9,    \ END SESSION

s1,
    #start: --->s2

s2,
        5':    --->s3
    #z4:     --->s3
s3,
    .01":    OFF^house;add p; set w(p) = ^HouseLightOff + T;

                add p; set w(p)= ^EndSession + T;
                add p; set w(p)=-987.987;
                add b; set a(b)= -99;
                add b; set a(b) = -987.987--->stopabortflush

s.s.3,  \Reinforcer controller - Dippers
s1,
        #start:    add p; set w(p)= ^StartSession+T;set x=100 --->s2
s2,
    x#T:     randd x=i;
             add j;show 3, Dippers,j;
             ON^dipper;z1;add p; set w(p)=T+^DipON--->s3
s3,
         .5":~ondur(MG, Box, 200);~;add p;set w(p)=T+^ToneOn1--->s4
s4,
    7.5":     z2; OFF^dipper; add p; set w(p)=T+^DipOff;
        if j = 30[@end, @noend]   \Change Here for Different # of Dippers
                      @end: z4 --->s1
                      @noend: z3 --->s2



\S.S.3 \Reinforcer Controller - Pellets
\s1,
\        #start:    add p; set w(p)=^StartSession+T;set x=100 --->s2
\s2,
\    x#T:     randd x=i;
\             add j;show 3,Pellets,j;
\             ON^Pellet;z1;add p; set w(p)=T+^Feed1+T;--->s3
\s2,
\        .05":     OFF^Pellet;--->s3
\s3,
\        .1":      ON^Pellet;add p;set w(p)=^Feed1+T; --->s4
\s4,
\        .05":     OFF^Pellet --->s5
\s5,
\    .3":    ~ondur(MG, BOX, 200);~;add p;set w(p)=T+^ToneOn1--->s6
\s6,
\    7.5":     z2; if j = 30[@end, @noend]   \Change Here for Different # of Dippers
\                      @end: z4 --->s1
\                      @noend: z3 --->s2 --->s2


S.S.4,  \Head Entry (Input 3) COUNTER
S1,
    #START:SHOW 5,HeadIn,0--->S2
S2,
    #R^headIN: add r; SHOW 5,HeadIn,1; add p; set w(p)=^PokeOn1+T---> sx


s.s.5,  \Dipper Up Timer (D variable)
s1,
    #z1:     set d=0--->s2
s2,
    0.1":    set d=d+0.1;show 2,DipUpTime,d--->sx
    #z2:    --->s1

s.s.6, \latency to first head poke after reinforcer
s1,
    #z1:--->s2
s2,
    #R^headIN: set b=b+1;set a(b)=d--->s1
    #z2:--->s1

S.S.7,  \records if there was a head entry after dipper presentation
s1,
    #start: --->s3
s2,
    .1":    if r>0 [@Ret, @noRet]
            @Ret: add s;show 4,ReinfRetr,s; --->s3
            @noRet:  --->s2
s3,
    #z1: set r=0 --->s2

S.S.8, \Session Duration Clock
s1,
    #start:--->s2
s2,
    1":    set c(0)=c(0)+.01; if c(0)>.59 [@AddMin, @NoAdd]
            @AddMin: add c(1); set c(0)=0; set c(2)=c(0)+c(1); show 1, Session, c(2) --->s2
            @NoAdd: set c(2)=c(0)+c(1); show 1, Session, c(2)--->s2

