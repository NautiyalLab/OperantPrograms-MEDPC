\                        C O M M E N T S
\
\ Progressive Ratio 2
\
\
\
\
\ Desrochers 6/4/19

\only save W, B variable to data file*******
DISKFORMAT = 13.1
DISKVARS = W, B    \"DISKVARS" only prints selected variables to datafile

                  \  Inputs ****************************
^headIN = 3       \  IR head entry detector
^LLever2 = 1       \  left lever press
^RLever2 = 2       \  right lever press


                  \  Outputs ****************************
^leftlever = 1
^rightlever = 2
^dipper = 3        \ On Liquid Dipper
^house = 7         \houselight

    \CONSTANTS***********************
^DipDuration = 5    \dipper up duration (sec)



\  VARIABLES USED IN THIS PROGRAM
\   a = lever press timer
\   b = progressive ratio number 
\   c = master timer (in seconds, with 1/10s resolution)
\   d = poking
\   e = 
\   f =
\   g = 
\   h = 
\   i = 
\   j = 
\   k = 
\   l
\   m = end session variable, intially set to 1, 2 = stopabortflush
\   n = 
\   o = 
\   p = trial time counter
\   q
\   r = 
\   s = lever record
\   t = master timer
\   u = poking
\   v = dipper count
\  (w) = time and event code for matlab
\   x = 
\   y = progressive ratio lever count
\   z (reserved)


\  \  SHOW POSITIONS
\  1 =
\  2 = Left Lever Responses
\  3 = Dips
\  4 = Session Time (minutes)
\  5 = head entries
\  6 = 

\======================================================
\ MATLAB standardized codes
\ *** = used in this program


^DipOn = 0025   \***
^DipOff = 0026   \***
^LLeverOn = 0027 \***
^RLeverOn= 0028  \***
^LLeverOff = 0029 \***
^RLeverOff = 0030 \***

^StartTrial = 0111
^EndTrial = 0112
^StartSession = 0113  \***
^EndSession = 0114   \***
^HouseLightOff = 0115
^HouseLightOn = 0116
^OnFan = 0117\***
^OffFan = 0118\***

^PokeOff1 = 1001
^PokeOff2 = 1002
^PokeOff3 = 1003
^PokeOff4 = 1004
^PokeOn1 = 1011
^PokeOn2 = 1012
^PokeOn3 = 1013
^PokeOn4 = 1014
^LLeverPress = 1015
^RLeverPress = 1016
^PokeOffStan = 1301
^PokeOnStan = 1302
^PokeOffComp = 1303
^PokeOnComp = 1304
^PokeOffCtrl = 1305
^PokeOnCtrl = 1306

^FeederLightOn = 0101  \***
^FeederLightOff = 0102  \***

\================================================================


\Dimensionalize Event and Time code array
dim w=10000

\===============================
\   Z-PULSES USED IN THIS PROGRAM
\ z1 = lever timer start
\ z3 = limit time after last press
\ z8/9/10 = head poking
\===============================


s.s.1,  \MATLAB TIMER "T"

s1,
        0": SET p = -1; set m = 1--->s2

s2,
        .02": SET T = T + 100000 --->sx \Keeps time in 50th of a second




S.S.2,    \INITIALIZE

s1,

           #start: ON^house; add p; set w(p)= ^HouseLightOn + T--->s2

s2,

         #z3: OFF^leftlever;add p; set w(p) = ^LLeverOff + T;--->s3


s3,
        10": OFF^house;add p; set w(p) = ^HouseLightOff + T;
            add p; set w(p)= ^EndSession + T;
            add p; set w(p)=-987.987;--->stopabortflush



s.s.3,  \TRIAL GENERATOR

s1,
        #start:add p; set w(p)= ^StartSession + T;
                ON^leftlever;
                add p; set w(p) = ^LLeverOn + T; set b=2--->s2

s2,
        #R^LLever2: add y; if y = b [@reward, @morepress]
			@reward: ON^dipper; add v; add p; set w(p) = ^DipOn + T; set b= b*2; set y=0; --->s3
			@morepress: --->sx 


s3,

        5":OFF^dipper; add p; set w(p) = ^DipOff + T ;SHOW 3, Dipper# ,v; --->s2



S.S.4,  \Head Entry (Input 3) COUNTER
S1,
    #START:SHOW 5,HeadIn,0; set u=0; --->S2
S2,
    #R^headIN:  add p; set w(p)=^PokeOn1+T;add u; SHOW 5,HeadIn,u; z10 ---> s3

s3,
    #z8 ---> s2


s.s.5,  \Head still in checker part 1
s1,
    #z10 --->s2   \Z10 gets called when head first enters

s2,
    0": z9 ---> s3

s3,
    0.05": if d>0 [@headSTILLin, @headExit]
        @headSTILLin: set d=0; ---> s2
        @headEXIT: z8; add p; set w(p)=^PokeOff1+T;---> s1



s.s.6, \Head still in checker part 2
s1,
    #z9: --->s2

s2,
    #R^headIN: set d=1; --->s3
s3, 
   0": --->s1



S.S.7, \Lever Press Record, and timer start

s1,
      #start: z1--->s2

s2,

      #R^LLever2: set a=0; z1; add s;show 2, LLever, s; add p; set w(p) = ^LLeverPress + T--->sx


S.S.8,  \Lever Press Timer
s1,
        #start: --->s2

s2,
        #z1: --->s3

s3,
	
	
	1": set a = a + 1; if a = 180 [@end, @continue]
		@end:z3--->sx
		@continue:--->sx



s.s.9,  \MASTER TIMER

s1,
        #start:--->s2

s2,
        1': set c = c + 1; show 4, SessTime, c;--->sx

s.s.10,   \Session Length Limit

s1,

       #start:--->s2

s2,

       120': OFF^leftlever;add p; set w(p) = ^LLeverOff + T;
	    OFF^house;add p; set w(p) = ^HouseLightOff + T;
            add p; set w(p)= ^EndSession + T;
            add p; set w(p)=-987.987;--->stopabortflush




